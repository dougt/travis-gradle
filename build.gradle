buildscript {
  repositories {
    mavenRepo name: 'clojars', url: 'http://clojars.org/repo'
  }
  dependencies {
    classpath 'org.clojars.myfreeweb.clojuresque:clojuresque:1.5.0-SNAPSHOT'
  }
}

allprojects {
  group = 'floatboth'
  version = '1.0.0'

  apply plugin: 'clojure'

  repositories {
    mavenCentral()
    clojarsRepo()
  }
}

subprojects {
  dependencies {
    compile 'org.clojure:clojure:1.3.0'
    testCompile 'midje:midje:1.3.1'
  }
}

project(':gradle-travis') {
  dependencies {
    compile project(':travis-gradle')
  }
}

// Aggregated documentation
dependencies {
  // clojuresque doesn't include clojure
  // subprojects documentation somehow works w/ compile dep
  clojuresque 'org.clojure:clojure:1.3.0'
}

clojuredoc.configure {
  destinationDir = new File('autodoc')
  // Allows subprojects which depend on others to work
  clojureRoots = subprojects.first().sourceSets.main.clojure
  // Source files list
  source = files {
    subprojects.collect {
      it.sourceSets.main.clojure
    }
  }
  compileClasspath = files {
    subprojects.collect {
      it.sourceSets.main.compileClasspath
    }
  }
  excludeNamespace('clojure*')
}
